(load "util.lisp")

(defun shift (grid shape dir)
  "Shift piece one unit in direction DIR if possible. Return new coords or nil."
  (let* ((dr (if (zerop dir) 1 0))
	 (dc dir)
	 (new-shape (mapcar #'(lambda (x)
				(list (+ dr (first x)) (+ dc (second x))))
			    shape)))
    (unless (or (some #'(lambda (x)
		      (or (not (<= 0 (second x) 6))
			  (>= (first x) (first (array-dimensions grid)))))
		  new-shape)
	    (some #'(lambda (x)
		      (= 1 (aref grid (first x) (second x))))
		  new-shape))
      new-shape)))

(defun drop (grid i j mx cycle &optional (sig nil sig-p))
  "Drop a shape until it rests. I is rock count, J is wind count, MX is
max height so far, and CYCLE is whether a cycle has been found. SIG
checks for a signature for repetition. Derive shape from I and position
of shape from MX, and return new grid, J, MX, and cycle."
  (let* ((is    (mod i *ns*))
	 (r     (- mx (+ 4 (- (nth is *shape-heights*) 1))))
	 (shape (mapcar #'(lambda (x)
			    (list (+ r (first x)) (+ 2 (second x))))
			(nth is *shapes*))))
    (while t
      (when-bind (val (shift grid shape (aref *wind* (mod j *nw*))))
	(setf shape val))
      (incf j)
      (let ((newshape (shift grid shape 0))) 
	(if newshape
	    (setf shape newshape)
	    (progn
	      (dolist (z shape)
		(setf (aref grid (first z) (second z)) 1))
	      (let ((new-mx (min mx (apply #'min (mapcar #'car shape)))))
		(when (and sig-p (null cycle))
		  (when-bind (val (funcall sig grid mx i j))
		    (setf cycle val)))
		(return-from drop (list grid j new-mx cycle)))))))))

(defun signature (&optional (n 20))
  "Return a function that converts last N rows of grid into a number.
Store results in MEMO with
key= (current-shape-index current-wind-index last-n-rows-number), and
value= (current-rock-num mx). When the inner function finds a
repetition, return values at the 2 indices, otherwise nil."
  (let ((memo (make-hash-table :test 'equal)))
    #'(lambda (grid mx i j)
	(let* ((rows (parse-integer
		      (format nil "窿祜镳骘骝镯磲ō眸瞟癌忮祜眸狃疱钿祜镳骘忮祜泔祆邈ㄡ蝈珧殇┅┅候徜轼博鲠ㄧ弭栳箬扉篝盹铙盹铟蝻黧礤盹┅ㄩ鲠扉篝鲠扉篝眸┅痱镧箦翩ㄧ弭栳箬扉篝盹铙盹铟蝻黧礤盹扉篝眸┅铋飑┅┅换换庆镡犰泔铙翎铘ㄤ彐疳蜥礤翦轭瘐舄磲ъ轶＇灬礅溽ㄣ狍ǎ芫暴ǎ芗暴┅疳蝮溽翎狈豇簪┅ㄤ彐疳蜥礤翦鏖钿磲脲狎蜥戾铉翳轭瘐舄洪铋糸犰泔铘孱趔轭瘐舄┅ㄤ彐疳蜥礤翦铟戾铉翳鏖钿┅ㄤ彐疳蜥礤翦箬狃弩Жè癌ò暴ò博ò畅è暴ū癌ū暴ū博ú暴è博ū博ú癌ú暴ú博è癌ū癌ú癌ǔ癌è癌ò暴ū癌ū暴┅ㄤ彐疳蜥礤翦铙戾铉翳箬狃弩┅ㄤ彐疳蜥礤翦箬狃瀛桢殓梏螵磲疸狎＇灬礅溽ūㄡ痧禊＇磲磲疸狎＇骈蝮┅┅箬狃弩┅ㄤ彐疳蜥礤翦磲蝻汶螵卑鞍鞍鞍鞍鞍癌戾舄è舶膊磲铛礅弪镦蝻汶麸潋镳ㄧ蜷磲脲狎蜥扉篝ǐ卑í穿珐洪铋糸犰屐屙孱癌眸ㄦ轵篝ㄡ蝌狴溟礤铙轱铙珧殇┅癌鏖钿泔躅ㄣ沆铋飑栳鲥麇孱泔躅翦蝈蝈痖糸镱镦翳箝珙狒躜弭疳螋铋飑箝箝珙狒躜濠蝻汶桢殓梏螬ㄤ雉轫弩ㄩ扉篝疳螋泫沆铒骘躅洧┅戾è鲠ㄤ蝻珧殇眸泫沆箝绌┅箦翩珧殇ㄦ轵篝鲠飑箦泔钿鲠飑眸翳轵鲠飑泫沆ㄦ秕螋鲠飑瘐箬眸蝻汶桢殓梏螬麒孱舶脖箦翩疳螋ōㄦ轵篝ㄡ蝌狴溟礤铙轱铙珧殇┅眸┅麒孱ㄡ钿疳螋泫沆濠ㄤ弩趄蹉趱蜷铉忾钿è蝻汶篝狎眸篝狎舂蝻汶孱眸孱洎泫沆戾舄è梏篝狎ōㄦ轵篝ㄡ蝌狴溟礤铙轱铙珧殇┅眸篝狎舂ㄨ翦钿ōㄦ轵篝ㄡ蝌狴溟礤铙轱铙珧殇┅眸孱洎ㄣ沆瀛桢殓梏ō梏孱梏篝狎舂ㄣ沆瀛戾铉翳ō蝻汶孱蝻汶篝狎舂┅眭祠轲戾鲠祯瀛忾钿铛憝泫沆弩蝈磲轭溴颟ㄦ祜矧ō磲蝻汶螵蝻汶篝狎舂泫沆瀛戾铉翳蝈趱蝾扉篝疳螋ǐí铛憝泫沆弩泫沆瀛桢殓梏ōㄦ轵篝ㄡ蝌狴溟礤铙轱铙珧殇┅铘ūǐ蝈磲轭溴蝻汶篝狎舂蝈鲥蝮蝻汶桢殓梏螬┅┅┅┅┅